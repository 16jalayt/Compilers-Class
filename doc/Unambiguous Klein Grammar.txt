------------------------------------------------------------------------|*|------------------------------------------------------------------------
				REFACTORED KLEIN GRAMMAR								|*|                          ORIGINAL KLEIN GRAMMAR
------------------------------------------------------------------------|*|------------------------------------------------------------------------
																		|*|
             <PROGRAM> ::= <DEFINITIONS>								|*| <PROGRAM> ::= <DEFINITIONS>
																		|*|
        <DEFINITIONS> ::= ε												|*| <DEFINITIONS> ::= ε
                        |  <DEF> <DEFINITIONS>							|*|					| <DEF> <DEFINITIONS>
																		|*|																						
				<DEF> ::= function <IDENTIFIER> ( <FORMALS> ) : <TYPE>  |*| <DEF> ::= function <IDENTIFIER> ( <FORMALS> ) : <TYPE>
                             <BODY>										|*|				<BODY>
																		|*|
            <FORMALS> ::= ε												|*| <FORMALS> ::= ε
                        | <NONEMPTYFORMALS>								|*|				| <NONEMPTYFORMALS>
																		|*|
    <NONEMPTYFORMALS> ::= <FORMAL>	<NEF-REST>							|*| <NONEMPTYFORMALS> ::= <FORMAL>
                        												|*|						| <FORMAL> , <NONEMPTYFORMALS>
   		   <NEF-REST> ::= , <NONEMPTYFORMALS>							|*|
  						| ε												|*|
																		|*|
             <FORMAL> ::= <IDENTIFIER> : <TYPE>							|*| <FORMAL> ::= <IDENTIFIER> : <TYPE>
																		|*|
               <BODY> ::= <PRINT-BODY>									|*| <BODY> ::= <PRINT-STATEMENT> <BODY>
                        | <EXPR>										|*|			  | <EXPR>
																		|*|
		 <PRINT-BODY> ::= <PRINT-STATEMENT> BODY						|*|							
																		|*|
               <TYPE> ::= integer										|*| <TYPE> ::= integer
                        | boolean										|*| 		 | boolean
																		|*| 
               <EXPR> ::= <SIMPLE-EXPR> <EXPR-REST>						|*| <EXPR> ::= <EXPR> < <SIMPLE-EXPR>
																		|*|				| <EXPR> = <SIMPLE-EXPR>
																		|*|				| <SIMPLE-EXPR>
		  <EXPR-REST> ::= < <EXPR>										|*|
		  			    | = <EXPR>										|*|
						| ε												|*|
																		|*|																																				|*|
        <SIMPLE-EXPR> ::= <TERM> <SIMPLE-EXPR-REST>						|*| <SIMPLE-EXPR> ::= <SIMPLE-EXPR> or <TERM>
                        												|*| 				| <SIMPLE-EXPR> + <TERM>
                        												|*| 				| <SIMPLE-EXPR> - <TERM>
                        												|*|					| <TERM>
   <SIMPLE-EXPR-REST> ::= or <SIMPLE-EXPR>								|*|
						| + <SIMPLE-EXPR>								|*|
						| - <SIMPLE-EXPR>								|*|
						| ε												|*|
																		|*|
               <TERM> ::= <FACTOR> <TERM-REST>							|*| <TERM> ::= <TERM> and <FACTOR>
                        												|*| 			| <TERM> * <FACTOR>
                        												|*| 			| <TERM> / <FACTOR>
                        												|*|				| <FACTOR>
																		|*|
		  <TERM-REST> ::= and <TERM>									|*| 
	     				| * <TERM>										|*|
		   				| / <TERM>										|*|
						| ε												|*|
																		|*|
             <FACTOR> ::= if <EXPR> then <EXPR> else <EXPR>				|*| <FACTOR> ::= if <EXPR> then <EXPR> else <EXPR>
                        | <NOT-FACTOR>									|*| 			| not <FACTOR>
                        | <IDENTIFIER-MAIN>								|*|				| <IDENTIFIER> ( <ACTUALS> )
                        | <LITERAL>										|*|				| <IDENTIFIER>
						| <NEG-FACTOR>									|*|				| <LITERAL>
                        | ( <EXPR> )									|*|				| - <FACTOR>
                      													|*|				| ( <EXPR> )
																		|*|
		 <NOT-FACTOR> ::= not <FACTOR>									|*|
																		|*|
		 <NEG-FACTOR> ::= - <FACTOR>									|*|
																		|*|
    <IDENTIFIER-MAIN> ::= <IDENTIFIER> <IDENTIFIER-REST>				|*|
																		|*|
    <IDENTIFIER-REST> ::= (<ACTUALS>)									|*|
						| ε												|*|
																		|*|
            <ACTUALS> ::= ε												|*| <ACTUALS> ::= ε
                        | <NONEMPTYACTUALS>								|*|				| <NONEMPTYACTUALS>		
																		|*|
    <NONEMPTYACTUALS> ::= <EXPR> <NEA-REST>								|*| <NONEMPTYACTUALS> ::= <EXPR>
                    													|*|						| <EXPR> , <NONEMPTYACTUALS>
   			<NEA-REST>::= , <NONEMPTYACTUALS>							|*|
   						| ε												|*|
																		|*|
            <LITERAL> ::= <NUMBER>										|*| <LITERAL> ::= <NUMBER>
                        | <BOOLEAN>										|*| 			| <BOOLEAN>
																		|*|
    <PRINT-STATEMENT> ::= print ( <EXPR> )								|*| <PRINT-STATEMENT> ::= print ( <EXPR> )
